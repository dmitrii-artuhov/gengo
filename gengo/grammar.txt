#######################
#   CURRENT VERSION   #
#######################

statements	    : statement NEWLINE (statement NEWLINE)*

statement		: expr
				: KEYWORD:return expr

expr		    : (KEYWORD:TYPE)? IDENTIFIER EQ expr
			    : comp-expr

comp-expr	    : arith-expr ((AND|OR|EQEQ|NE|GT|GTE|LT|LTE) arith-expr)*

arith-expr	    : term ((PLUS|MINUS) term)*

term		    : factor ((MUL|DIV) factor)*

factor		    : INT|FLOAT|STRING|IDENTIFIER
				: IDENTIFIER LPAREN (expr (COMMA expr)*)? RPAREN
				: IDENTIFIER LSQUARE expr RSQUARE (LSQUARE expr RSQUARE)* (EQ expr)?
			    : (PLUS|MINUS) factor
			    : LPAREN comp-expr RPAREN
			    : NOT comp-expr
				: array-expr
				: if-expr
				: for-expr
				: func-def

func-def		: KEYWORD:function IDENTIFIER LPAREN (IDENTIFIER COLON KEYWORD:TYPE
				  (COMMA IDENTIFIER COLON KEYWORD:TYPE)*)? RPAREN ARROW KEYWORD:TYPE
				  LBRACE statements RBRACE

if-expr         : KEYWORD:if LPAREN expr RPAREN LBRACE statements RBRACE
                  (if-else-expr|else-expr)?
              
if-else-expr    : KEYWORD:elif LPAREN expr RPAREN LBRACE statements RBRACE
                  (if-else-expr|else-expr)?

else-expr       : KEYWORD:otherwise LBRACE statements RBRACE

for-expr		: KEYWORD:loop LPAREN (expr)? NEWLINE expr NEWLINE (expr)? RPAREN
				  LBRACE statements RBRACE

array-expr		: LSQUARE (expr (COMMA expr)*)? RSQUARE

